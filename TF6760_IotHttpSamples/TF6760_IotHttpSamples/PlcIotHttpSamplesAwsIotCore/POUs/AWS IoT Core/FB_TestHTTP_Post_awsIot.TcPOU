<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="FB_TestHTTP_Post_awsIot" Id="{b41a389a-f33a-4d95-86ff-f78c99b4112c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TestHTTP_Post_awsIot
VAR_INPUT
	bSend				: BOOL;
END_VAR
VAR_IN_OUT
	fbClient			: FB_IotHttpClient;
END_VAR
VAR_OUTPUT
	bBusy				: BOOL;
	bError				: BOOL;
END_VAR

VAR	
	fbRequest			: FB_IotHttpRequest;
	fbJson				: FB_JsonDomParser;
	fbPayload   		: FB_JsonSaxWriter;
	nState				: UDINT;
	RisingEdge			: R_TRIG;
	
	bGetContentResult	: BOOL;
	sContent			: STRING(511);
	
	bGetJsonResult		: BOOL;
	jsonDoc				: SJsonValue;
	jsonVal				: SJsonValue;
	sResultValue		: STRING;
	
	nReqCount			: UDINT;
	nResCount			: UDINT;
	nValidResCount		: UDINT;
	nErrCount			: UDINT;	
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[RisingEdge(CLK:= bSend);
CASE nState OF
0:		
	IF RisingEdge.Q THEN
		fbPayload.StartObject();
		fbPayload.AddKey('message');
		fbPayload.AddReal(42.42);
		fbPayload.EndObject();
		sContent:= fbPayload.GetDocument();
		fbPayload.ResetDocument();
		IF fbRequest.SendRequest(sUri:='/topics/mytopic?qos=1', fbClient:= fbClient, eRequestType:= ETcIotHttpRequestType.HTTP_POST, pContent:= ADR(sContent), nContentSize:= LEN2(ADR(sContent)), 0) THEN				
			nState:= 1;
			nReqCount:= nReqCount+1;
			bBusy:= TRUE;
			bError:= FALSE;
		END_IF					
	END_IF
1:
	IF NOT fbRequest.bBusy THEN
		bError:= TRUE;
		IF NOT fbRequest.bError THEN				 				
			bGetContentResult:= fbRequest.GetContent(pContent:=ADR(sContent), nContentSize:=SIZEOF(sContent), bSetNullTermination:=TRUE);	
			IF fbRequest.nStatusCode >= 200 AND fbRequest.nStatusCode < 300 THEN
				bGetJsonResult:= FALSE;
				jsonDoc:= fbRequest.GetJsonDomContent(fbJson);				
				IF jsonDoc <> 0 THEN
					bGetJsonResult:= TRUE;
					IF fbJson.HasMember(jsonDoc, 'message')  THEN
						jsonVal:= fbJson.FindMember(jsonDoc, 'message');
						sResultValue:= fbJson.GetString(jsonVal);
						IF sResultValue = 'OK' THEN
							nValidResCount:= nValidResCount+1;
							bError:= FALSE;
						END_IF
					END_IF		
				END_IF
				nResCount:= nResCount+1;					
			END_IF				
		END_IF
		nState:= 0;
		bBusy:= FALSE;
		IF bError THEN
			nErrCount:= nErrCount+1;
		END_IF		
	END_IF  	
END_CASE

]]></ST>
    </Implementation>
    <LineIds Name="FB_TestHTTP_Post_awsIot">
      <LineId Id="99" Count="2" />
      <LineId Id="120" Count="0" />
      <LineId Id="764" Count="1" />
      <LineId Id="770" Count="0" />
      <LineId Id="776" Count="0" />
      <LineId Id="531" Count="0" />
      <LineId Id="778" Count="0" />
      <LineId Id="297" Count="0" />
      <LineId Id="129" Count="1" />
      <LineId Id="575" Count="1" />
      <LineId Id="131" Count="0" />
      <LineId Id="150" Count="2" />
      <LineId Id="667" Count="0" />
      <LineId Id="153" Count="0" />
      <LineId Id="163" Count="1" />
      <LineId Id="623" Count="0" />
      <LineId Id="615" Count="2" />
      <LineId Id="524" Count="2" />
      <LineId Id="663" Count="1" />
      <LineId Id="668" Count="0" />
      <LineId Id="662" Count="0" />
      <LineId Id="536" Count="0" />
      <LineId Id="188" Count="0" />
      <LineId Id="190" Count="0" />
      <LineId Id="193" Count="0" />
      <LineId Id="196" Count="1" />
      <LineId Id="577" Count="0" />
      <LineId Id="670" Count="1" />
      <LineId Id="669" Count="0" />
      <LineId Id="198" Count="1" />
      <LineId Id="201" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>